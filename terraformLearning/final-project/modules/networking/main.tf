resource "aws_vpc" "this" {
    cidr_block           = var.vpc_cidr
    enable_dns_support   = true
    enable_dns_hostnames = true

    tags = {
        Name         = "${var.project_name}-vpc"
        Environment  = var.environment
    }
}



# -------- PUBLIC SUBNETS -------------------------------------
resource "aws_subnet" "public" {
    count              = length(var.public_subnet_cidrs)
    vpc_id             = aws_vpc.this.id
    cidr_block         = var.public_subnet_cidrs[count.index]
    availability_zone  = var.availability_zones[count.index]

    # Instances launched here, automatically get a public IP
    map_public_ip_on_launch = true


    tags = {
        Name         = "${var.project_name}-public-${count.index + 1}"
        Environment  = var.environment
        Tier         = "public"
    }
}


resource "aws_internet_gateway" "this" {
    vpc_id = aws_vpc.this.id

    tags = {
        Name         = "${var.project_name}-ig"
        Environment  =  var.environment
    }
}


resource "aws_route_table" "public" {
  vpc_id = aws_vpc.this.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.this.id
  }

  tags = {
    Name        = "${var.project_name}-public-rt"
    Environment = var.environment
  }
}

resource "aws_route_table_association" "public" {
  count          = length(var.public_subnet_cidrs)
  subnet_id      = aws_subnet.public[count.index].id
  route_table_id = aws_route_table.public.id
}